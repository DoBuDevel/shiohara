buildscript {
	ext {
		springBootVersion = '1.5.10.RELEASE'
		thymeleafVersion = '3.0.3.RELEASE'
		hibernateVersion = '5.6.1.Final'
		jerseyVersion = '2.25.1'
		httpClientVersion = '4.5.2'
	}
	repositories {
		mavenCentral()
		 jcenter()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath "com.eriwen:gradle-js-plugin:2.12.0"
	}
}


apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.eriwen.gradle.js'

group = 'com.viglet.shiohara'
version = '1.0-SNAPSHOT'

description = "viglet-shiohara"

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

// Combine JS files
task combineSystemJs(type: com.eriwen.gradle.js.tasks.CombineJsTask) {
    source = "${projectDir}/src/main/resources/js/client-side/src/system"
    dest = "${projectDir}/src/main/resources/js/client-side/build/shSystem.js"
}

task combineFeatureJs(dependsOn: ':combineSystemJs', type: com.eriwen.gradle.js.tasks.CombineJsTask) {
    source = "${projectDir}/src/main/resources/js/client-side/src/feature"
    dest = "${projectDir}/src/main/resources/js/client-side/build/shFeature.js"
}

task combineAllJs(dependsOn: ':combineFeatureJs', type: com.eriwen.gradle.js.tasks.CombineJsTask) {
    source = ["${projectDir}/src/main/resources/js/client-side/src/main/__shFactory.js", "${projectDir}/src/main/resources/js/client-side/build/shSystem.js", "${projectDir}/src/main/resources/js/client-side/build/shFeature.js"]
    dest = "${projectDir}/src/main/resources/js/client-side/build/shiohara.js"
}

task minifyAllJs(dependsOn: ':combineAllJs', type: com.eriwen.gradle.js.tasks.MinifyJsTask) {
 	source = "${projectDir}/src/main/resources/js/client-side/build/shiohara.js"
    dest = file("${projectDir}/src/main/resources/public/js/dist/shiohara.min.js")
    closure {
        warningLevel = 'QUIET'
    }
}

task stage(dependsOn: ['build', 'clean'])

compileJava.dependsOn(minifyAllJs)

build.mustRunAfter clean

jar {
    baseName = 'shiohara'
    version =  '0.1.0'
}

springBoot {
    executable = true
}

repositories {   
     mavenCentral()
}
dependencies {

	// Spring Boot
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version:"${springBootVersion}"
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-parent', version:"${springBootVersion}"
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-jersey', version:"${springBootVersion}"
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-activemq', version:"${springBootVersion}"
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: "${springBootVersion}"
    compile(group: 'org.springframework.boot', name: 'spring-boot-starter-thymeleaf', version: "${springBootVersion}") {
		exclude(module: 'thymeleaf-layout-dialect')
    }
   compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-hibernate5', version: '2.9.4'
   
    // Glassfish
    compile group: 'org.glassfish.jersey.media', name: 'jersey-media-multipart', version:"${jerseyVersion}"
    compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet-core', version:"${jerseyVersion}"
    
    // Thymeleaf
    compile group: 'org.thymeleaf', name: 'thymeleaf-spring4', version:"${thymeleafVersion}"
    compile group: 'org.thymeleaf', name: 'thymeleaf', version:"${thymeleafVersion}"
    
    // HikariCP   
    compile group: 'com.zaxxer', name: 'HikariCP', version:'2.6.0'
    
    // HSQLDB
    compile group: 'org.hsqldb', name: 'hsqldb', version:'2.4.0'
    
    // JSON
    compile group: 'org.json', name: 'json', version:'20140107'
    
    // Apache Commons
    compile group: 'commons-logging', name: 'commons-logging', version:'1.2'
    compile group: 'org.apache.commons', name: 'commons-compress', version: '1.16.1'   
    compile group: 'commons-io', name: 'commons-io', version:'2.5'
    compile group: 'commons-lang', name: 'commons-lang', version:'2.6'
    
    // HTTP Client
    compile(group: 'org.apache.httpcomponents', name: 'httpclient', version:"${httpClientVersion}") {
		exclude(module: 'commons-logging')
    }
    compile group: 'org.apache.httpcomponents', name: 'httpmime', version:"${httpClientVersion}"
    
    // Log4J
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version:'2.7'
    
    // JSoup
	compile group: 'org.jsoup', name: 'jsoup', version: '1.8.3'
	
	// Hibernate
	compile group: 'org.hibernate', name: 'hibernate-search-engine', version: "${hibernateVersion}"
	compile group: 'org.hibernate', name: 'hibernate-search-orm', version: "${hibernateVersion}"
	
	// Swagger 2
	compile 'io.springfox:springfox-swagger2:2.7.0'
	compile 'io.springfox:springfox-swagger-ui:2.7.0'
	
	// WebJars
	compile 'org.webjars.bower:jquery:1.11.1'
	compile 'org.webjars:bootstrap:3.3.7-1'
	compile 'org.webjars.bower:moment:2.20.1'
	compile 'org.webjars.bower:angular:1.6.9'
	compile 'org.webjars.bower:ace-builds:1.3.1'
	compile 'org.webjars.bower:tinymce:4.5.8'
	compile 'org.webjars.bower:angular-ui-router:1.0.14'
	compile 'org.webjars.bower:angular-translate:2.17.0'
	compile 'org.webjars.bower:angular-resource:1.6.9'
	compile 'org.webjars.bower:angular-animate:1.6.9'
	compile 'org.webjars.bower:angular-sanitize:1.6.9'
	compile 'org.webjars.bower:angular-bootstrap:2.5.0'
	compile 'org.webjars.bower:angular-cookies:1.6.9'
	compile 'org.webjars.bower:angular-moment:1.2.0'
	compile 'org.webjars.bower:angular-ui-ace:0.2.3'
	compile 'org.webjars.bower:font-awesome:4.7.0'
	compile 'org.webjars.bower:ng-file-upload:12.2.13'
	compile 'org.webjars.bower:angular-ui-notification:0.3.6'
	compile 'org.webjars.bower:angular-ui-tinymce:0.0.19'
}
